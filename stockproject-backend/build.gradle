// 모든 하위 프로젝트에 적용할 수 있도록 플러그인을 준비시킵니다.
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

// 'subprojects'는 settings.gradle에 include된 모든 하위 모듈을 의미합니다.
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 공통 그룹 ID와 버전을 설정합니다.
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    // Java 17 버전을 사용하도록 설정합니다.
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    // 소스 코드의 인코딩을 UTF-8로 설정합니다.
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    // 의존성을 다운로드할 중앙 저장소를 지정합니다.
    repositories {
        mavenCentral()
    }

    // Spring Cloud 버전을 변수로 선언
    ext {
        set('springCloudVersion', "2025.0.0")
    }

    // 모든 하위 프로젝트가 이 버전 정보를 공유하게 됨
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    // 모든 모듈의 테스트는 JUnit5 플랫폼을 사용하도록 설정합니다. test시 eureka false
    tasks.named('test') {
        useJUnitPlatform()
        systemProperty 'eureka.client.enabled', 'false'
    }
}